Program : PROGRAM id ';' OptionalVarList FunctionList Main

OptionalVarList : VarSection
                | 

FunctionList : FunctionList FunctionListElem
             | FunctionListElem
             | 

FunctionListElem : FUNCTION FuncDefinition ':' id ';' OptionalVarList FunctionBody
                 | PROCEDURE FuncDefinition ';' OptionalVarList FunctionBody

FuncDefinition : id Parameters
              
Parameters : '(' PairList ')'
           | 

PairList : PairList ';' PairListElem
         | PairListElem
         | 

PairListElem : id ':' id

FunctionBody : BEGIN Body END ';'

Main: OptionalVarList MainSection

VarSection : VAR VarsList
           |  

VarsList : VarsList VarsListElem
         | VarsListElem

VarsListElem : VarsListElemIDs ':' VarType ';'

VarType : id
        | ARRAY ArrayIndexes OF id

ArrayIndexes : '[' num '.' '.' num ']'
             | 

VarsListElemIDs : VarsListElemIDs ',' id
                | id

MainSection : BEGIN Body END '.'

Body : Body BodyElem
     | BodyElem 

BodyElem : Func OptionalSemiColon
         | Atrib OptionalSemiColon
         | FORStatement OptionalSemiColon
         | IFStatement OptionalSemiColon
         | WHILEStatement OptionalSemiColon

Func : id '(' ArgsList ')' 

ArgsList : ArgsList ',' Arg
         | Arg
         | 

Arg : '(' Arg ')'
    | string
    | Exp


FORStatement : FOR Atrib FORTo NumExp DO FORBody

FORTo : TO
      | DOWNTO

FORBody : FORStatement
        | BEGIN Body END OptionalSemiColon
        | BodyElem

IFStatement : IF Condition THEN BodyElem ELSE BodyElem
            | IF Condition THEN BodyElem

WHILEStatement : WHILE Condition DO WHILEBody

WHILEBody : WHILEStatement
          | BEGIN Body END OptionalSemiColon
          | BodyElem

Condition : '(' Condition ')'
          | LogicExp

Atrib : IDWrapper ':' '=' Exp

IDWrapper : '(' IDWrapper ')'
          | ArrayElem
          | id

Exp : NumExp
    | BooleanExp
    | LogicExp


NumExp : NumExp '+' NumTerm
       | NumExp '-' NumTerm
       | NumTerm

NumTerm : NumTerm '*' NumFactor
        | NumTerm '/' NumFactor
        | NumTerm DIV NumFactor
        | NumTerm MOD NumFactor
        | NumFactor

NumFactor : '(' NumExp ')'
          | num
          | ArrayElem
          | id
          | Func


ComparationSymbol : '>'
                  | '<'
                  | '=' 
                  | '>' '='
                  | '<' '='

BooleanExp : BooleanExp ComparationSymbol BooleanExp
           | BooleanFactor

BooleanFactor : '(' Exp ')'
              | boolean
              | num
              | string
              | ArrayElem
              | id
              | Func

LogicExp : LogicExp OR LogicTerm
         | LogicTerm

LogicTerm : LogicTerm AND LogicNot
          | LogicNot

LogicNot : NOT LogicFactor
         | LogicFactor

LogicFactor : '(' LogicExp ')'
            | BooleanExp

OptionalSemiColon : ';'
                  | 

ArrayElem : id '[' Exp ']'
